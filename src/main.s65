.include "constants.incl"

.import oam, palette
.importzp gamepad, nmi_ready, temp16bit_ptr, temp_8_bit_holder, temp_byte, temp_64_byte_storage, byte_holder

.segment "HEADER"
INES_MAPPER 	= 0
INES_MIRROR 	= 0
INES_RAM	= 0

.byte "NES", $1A
.byte $02 		; 16KB PRG bank count
.byte $00 		; CHR RAM (no CHR ROM)
.byte INES_MIRROR | (INES_RAM << 1) | ((INES_MAPPER & $F) << 4)
.byte INES_MAPPER & $F0
.byte $0, $0, $0, $0, $0, $0, $0, $0

.import vblank

.segment "VECTORS"
.word vblank
.word reset
.word irq

.segment "RODATA"
tiles_chr: .incbin "../data/NESlides.chr"

group_table:
    .byte $0F, $07, $0B, $03

.RODATA
default_palette:
.byte $0F,$15,$26,$37
.byte $0F,$09,$19,$29
.byte $0F,$01,$11,$21
.byte $0F,$00,$10,$30
.byte $0F,$18,$28,$38
.byte $0F,$14,$24,$34
.byte $0F,$1B,$2B,$3B
.byte $0F,$12,$22,$32

welcome_txt:
.byte "HELLO WORLD", 0

.segment "CODE"
.proc copy_tiles_chr
  lda #<tiles_chr                           ; load the source address into a pointer in zero page
  sta temp16bit_ptr
  lda #>tiles_chr
  sta temp16bit_ptr+1

  ldy #0                                    ; starting index into the first page
  sty PPU_MASK                              ; Turn off rendering just in case
  sty PPU_VRAM_ADDRESS2                     ; Set the destination address high byte
  sty PPU_VRAM_ADDRESS2                     ; Set the destination address low byte
  ldx #32                                   ; number of 256-byte pages to copy
loop:
  lda (temp16bit_ptr),y                     ; copy one byte
  sta PPU_VRAM_IO
  iny
  bne loop                                  ; repeat until we finish the page
  inc temp16bit_ptr+1                       ; go to the next page
  dex
  bne loop                                  ; repeat until we've copied enough pages
  rts
.endproc

.segment "CODE"
.proc get_tile_position
  tay                                   ; Save the value for later
  lsr a                                 ; Shift right to move the high nibble into the low nibble position
  lsr a                                 ; Repeat
  lsr a                                 ; Repeat
  lsr a                                 ; Now the high nibble is in the lower 4 bits
  tax                                   ; Transfer the accumulator (high nibble) to the X register  
  tya                                   ; Reload the original value into the accumulator
  and #$0F                              ; Mask out the high nibble to keep only the low nibble
  asl a                                 ; Same thing but other way around
  asl a
  asl a
  asl a
  tay                                   ; Transfer the accumulator (low nibble) to the Y register
  rts
.endproc

.proc upscale_text_data
  ; Input: A in the accumulator (e.g., 0x6A = 0110 1010)
  ; Output: creates two bytes from the left nibble and right nible => 0011 1100 and 1100 1100
  ; Stores both bytes into temporary ZP respectively in y and y + 32
  ; Also duplicates the bytes vertically so stored in ZP respectively y+1 and y+1 + 32

  sta byte_holder                       ; Store the byte we will modify
  sed                                   ; Set the D flag
  dex                                   ; Decrease X because X starts at 16 but we want values between 0-15

; Weird stuff to map Y to the correct offset in ZP 
; If you seperate the 8x8 tiles into 4 2x2. top left = 1, top right = 2, bottom left = 3, bottom right = 4 (can also be seen as 4 chunks of 4 bytes of data)
; I want to put into ZP the tiles in order 1 - 3 - 2 - 4, because it's easier to do math later
  stx $01                               ; Store X in temporary location
  lda $01                               ; Copy X to A for manipulation
  lsr                                   ; Divide A by 4 (get the group number in bits 0–1)
  lsr                                   ; (each lsr shifts 1 bit right)
  tay                                   ; Store group number in Y
  lda group_table,y                     ; Look up base offset for the group
  tay                                   ; Store the base offset in Y
  lda $01                               ; Reload X into A
  and #%00000011                        ; Keep the two lsb to get the value of the offset in base 4 (0–3)
  sta $01                               ; Store it for later use (Don't need X anymore)
  tya                                   ; Transfer Y to A for the substraction
  sec                                   ; Set Carry
  sbc $01                               ; Substract Y from A where Y is the the offset in base 4 and A is the Offset of the group table
  asl                                   ; Shift Left to double it because we want to duplicate vertically and write to y+1 so a vale of 1 should not write to y = 1 but y = 2 so that you dont overwite the y+1 from previous iteration
  tay                                   ; Transfer A to Y for use at the end

;can move this down to flag_is_clear since i check at the end anyways just dont remove the jmp:+ because we still need to skip that part the first time around
begin:
  php                                   ; Push the status register onto the stack
  pla                                   ; Pull it back into the accumulator
  and #%00001000                        ; Mask out the D flag
  beq flag_is_clear                     ; Branch if the D flag is not set
  lda byte_holder                       ; Load the byte we need to manipulate
  and #$F0                              ; Mask out the low nibble, keeping only the high nibble for this loop iteration
  jmp :+                                ; Skip over the flag_is_clear
flag_is_clear:
  tya                                   ; Transfer Y to A for addition
  clc                                   ; Clear Carry
  adc #$20                              ; Add an offset of 32 for chunk 2 and 4 to A
  tay                                   ; Transfer A back to Y

  lda byte_holder                       ; Load the byte we need to manipulate
  and #$0F                              ; Mask out the high nibble, keeping only the low nibble for this loop iteration
:

  sta temp_8_bit_holder                 ; Store the nibble for later use
  clc                                   ; Clear carry we will use it to know what value each bit in the nibble has
  lda #$00                              ; Load zero into the accumulator
  sta temp_byte                         ; Store zero directly into `temp_byte`, temp_byte will be the final modified byte so we reset it here (though it might not be needed since we wright all 8 bits to it)

  ldx #4                                ; Using X to iterate over the nibble's 4 bits
loop:
  lsr temp_byte                         ; Right shift to set a zero in msb
  lsr temp_byte                         ; Right shift again to set another zero in msb (we rotate from memory here because lsr and asl change the carry with msb/lsb)

  php                                   ; Push the status register onto the stack
  pla                                   ; Pull it back into the accumulator
  and #%00001000                        ; Mask out the D flag
  beq second_check_d_flag_clear         ; Branch if the D flag is not set
  lda temp_8_bit_holder                 ; Load the nibble
  rol                                   ; Rotate left to put msb in Carry flag (case left nibble)
  jmp :+                                ; Skip over the flag clear
second_check_d_flag_clear:
  lda temp_8_bit_holder                 ; Load the nibble
  ror                                   ; Rotate right instead to put lsb in Carry flag (case right nibble)
:
  
  sta temp_8_bit_holder                 ; Store rotated nibble back in temp
  lda temp_byte                         ; Load the byte we will modify
  bcc :+                                ; If lsb/msb is 0 skip over because we already added two zeroes in temp_byte with lsr
  ora #$C0                              ; Replace the two zeroes with ones -> 0000 0000 || 1100 0000 to temp_byte
:

  sta temp_byte                         ; Store the change
  dex                                   ; Decrease X
  cpx #$00                              ; Set/Clear zero flag
  bne loop                              ; if not Zero loop back for the other bits of the nibble
  
  php                                   ; Push the status register onto the stack
  pla                                   ; Pull it back into the accumulator
  and #%00001000                        ; Mask out the D flag
  beq do_not_invert                     ; Skip if the D flag is not set
                                        ; Invert the byte
  lda #$00                              ; Start with a cleared accumulator for the result
  sta temp_8_bit_holder                 ; temp_8_bit_holder storage for the reversed bits
  ldx #$08                              ; Set up loop counter for 8 bits

reverse_loop:
  asl temp_byte                         ; Shift the leftmost bit of input_byte into the carry
  ror temp_8_bit_holder                 ; Rotate the carry bit into the result (temp_8_bit_holder)
  dex                                   ; Decrement the loop counter
  bne reverse_loop                      ; Repeat until all 8 bits are processed
  lda temp_8_bit_holder                 ; Load the reversed byte back into A
  jmp :+                                ; Skip over the empty byte
do_not_invert:
  lda temp_byte                         ; Load the byte
:
  sta temp_64_byte_storage,y            ; Nibble as been turned into a byte we now store it in ZP + offset Y
  sta temp_64_byte_storage+1,y          ; Store a copy of it so that we also have a vertical duplication and not just horizontal

  php                                   ; Push the status register onto the stack
  pla                                   ; Pull it back into the accumulator
  and #%00001000                        ; Mask out the D flag
  beq third_check_d_flag_clear          ; Branch if the D flag is not set
  cld                                   ; If we did the Left nibble clear D flag and repeat for the right nibble
  jmp begin                             ; Loop back for the other nibble
third_check_d_flag_clear:

  rts                                   ; Return from Subroutine
.endproc
.proc temp_map_new_VRAM_address
; Temporary code to write to new CHR RAM address, this should be replaced by proper mapping for now I just hard code one location
; Set the target PPU VRAM address (temporary code we need a map to know where we can write the info in CHR RAM here I'm hard coding it to start at adress 0C00)
  lda #$0C              
  sta PPU_VRAM_ADDRESS2                 ; Write high byte to PPU address register
  lda #$00              
  sta PPU_VRAM_ADDRESS2                 ; Write low byte to PPU address register

  ldx #$40                              ; How many bits we need to transfer from ZP to VRAM
  ldy #$00                              ; Offset
loop:  
  lda temp_64_byte_storage,y            ; Load the upscaled info from ZP into accumulator
  sta PPU_VRAM_IO                       ; Write it to the VRAM (this one is will automatically increment which is why the weird chunk division was done, because I don't know how to change the exact byte I want in VRAM without the automatic inrement)
  iny                                   ; Increment offset
  dex                                   ; Decrease X
  cpx #$00                              ; Check if X == 0
  bne loop                              ; If not keep looping

  rts                                   ; Return from Subroutine
.endproc

.proc test_create_upscale

  lda #$47                              ; Load the value $59 into the accumulator this will be replaced by reading whatever text we want
  jsr get_tile_position                 ; Jump to subroutine to Get X and Y of the tile

  ; Load the source address of tiles_chr into a temporary pointer in zero-page
  lda #<tiles_chr                       ; Load low byte of tiles_chr address
  sta temp16bit_ptr                     ; Store it in temp16bit_ptr
  lda #>tiles_chr                       ; Load high byte of tiles_chr address
  sta temp16bit_ptr+1                   ; Store it in temp16bit_ptr+1

set_tile_row:                           ; Move the pointer to point to the correct Y location (simple for loop)
  cpx #$00                              ; if x == 0
  beq :+                                ; exit loop
  inc temp16bit_ptr+1                   ; else increase pointer column
  dex                                   ; Decrease X
  jmp set_tile_row                      ; Loop Back
:

 
  ; Copy 16 bytes 
  ; 16bytes is the total information in a tile
  ; 8x8 pixels is 64 pixels, each pixel is two bits of information, total is 128 bits which is 16 bytes
  ; I'm not sure what the bits represents exactly as the two bits per pixel tell which color in the palette table to take
  ; However the first byte (byte 0) does not contain the info of 4 pixels but rather half the info of the first 8 pixels
  ; The 9th byte (byte 8) contains the second half of the first 8 pixels
  ; Copying the 9th byte into memory using the latch will put set it automatically as the second half information of the pixel. 
  ; If you iterate step by step over the loop you will notice that the "shape" is copied first with the previous second half information overlaying
  ; And starting at byte 8 it will overlap the "shape" and now give it the second half of the information. which gives it the proper "color"
  ldx #$10                              
copy_loop:
  ; Push X and Y to the stack
  txa                                   ; Transfer X to A
  pha                                   ; Push A onto the stack
  tya                                   ; Transfer Y to A
  pha                                   ; Push A onto the stack 

  lda (temp16bit_ptr),y                 ; Load a byte from tiles_chr using indirect Y addressing (adjusting the X position of the tile, don't forget tile one starts at bit 16 not 1)
  jsr upscale_text_data                 ; Jump to subroutine duplicating and expanding the bytes

  ; Pull Y and X back from the stack
  pla                                   ; Pull A from the stack (was Y)
  tay                                   ; Transfer A to Y
  pla                                   ; Pull A from the stack (was X)
  tax                                   ; Transfer A to X

  iny                                   ; Increment Y to move to the next byte
  dex                                   ; Decrement X (number of bytes remaining to copy)
  bne copy_loop                         ; Repeat until X reaches zero

  jsr temp_map_new_VRAM_address         ; Now write the info from ZP to VRAM

  rts                                   ; Return from the subroutine
.endproc
.CODE
irq:
	rti

.proc reset
	sei 			; disable interrupts
	lda #0
	sta PPU_CONTROL
	sta PPU_MASK
	sta APU_DM_CONTROL
	lda #$40
	sta JOYPAD2

	; Disable decimal mode
	cld
	ldx #$FF
	txs

	; Wait for V-Blank
	bit PPU_STATUS
wait_vblank:
	bit PPU_STATUS
	bpl wait_vblank

	lda #0
	ldx #0
clear_ram:
	sta $0000, x
	sta $0100, x
	sta $0200, x
	sta $0300, x
	sta $0400, x
	sta $0500, x
	sta $0600, x
	sta $0700, x
	inx
	bne clear_ram

	lda #$FF
	ldx #0
clear_oam:
	sta oam, x
	inx
	inx
	inx
	inx
	bne clear_oam

wait_vblank2:
	bit PPU_STATUS
	bpl wait_vblank2

    ; Copy CHR tiles into CHR RAM
	jsr copy_tiles_chr
	
	; testing write to CHR RAM
	jsr test_create_upscale

	; Enable V-Blank
	lda #%10001000
	sta PPU_CONTROL

	jmp main
.endproc

.proc ppu_signal
	sta nmi_ready

loop:
	lda nmi_ready
	bne loop

	rts
.endproc

.proc ppu_update
	lda #NMI_SIG_RENDERING_ON
	
	jmp ppu_signal
.endproc

.proc ppu_off
	lda #NMI_SIG_RENDERING_OFF

	jmp ppu_signal
.endproc

.proc clear_nametable
	lda PPU_STATUS

	; Set PPU address to first nametable address
	lda #>PPU_ADDR_NT_0
	sta PPU_VRAM_ADDRESS2
	lda #<PPU_ADDR_NT_0
	sta PPU_VRAM_ADDRESS2

	; Clear the name table
	lda #0
	ldy #30
row_loop:
	ldx #32

col_loop:
	sta PPU_VRAM_IO
	dex
	bne col_loop

	dey
	bne row_loop

	; Clear attribute table
	ldx #64
loop:
	sta PPU_VRAM_IO
	dex
	bne loop

	rts
.endproc

.proc gamepad_poll
	; Prepare for read
	lda #1
	sta JOYPAD1
	lda #0
	sta JOYPAD1

	ldx #8
loop:
	pha
	lda JOYPAD1

	and #%00000011
	cmp #%00000001
	pla

	ror
	dex
	bne loop
	sta gamepad
	rts
.endproc

.proc main
	ldx #0

paletteloop:
	lda default_palette, x
	sta palette, x
	inx
	cpx #32
	bcc paletteloop

	jsr clear_nametable

	lda PPU_STATUS
	lda #>PPU_ADDR_NT_0
	sta PPU_VRAM_ADDRESS2
	lda #<PPU_ADDR_NT_0
	sta PPU_VRAM_ADDRESS2

	ldx #0
text_loop:
	lda welcome_txt, x
	sta PPU_VRAM_IO
	inx
	cmp #0 ; stop at null terminator
	beq :+
	jmp text_loop
:
	
	; Temporary code to show the upscaled Letter
	; lda PPU_STATUS
	; lda #$07              
    ; sta PPU_VRAM_ADDRESS2                 ; Write high byte to PPU address register
    ; lda #$60              
    ; sta PPU_VRAM_ADDRESS2                 ; Write low byte to PPU address register

    ldx #255                                ; Just padding
padding:
	lda #$00
	sta PPU_VRAM_IO
	dex
	cpx #0
	beq :+
	jmp padding
:

	lda #$C0
	sta PPU_VRAM_IO

	lda #$C2
	sta PPU_VRAM_IO

    lda #$C0
	sta PPU_VRAM_IO

	lda #$C2
	sta PPU_VRAM_IO

    ldx #28                                ; Just padding
more_padding:
	lda #$00
	sta PPU_VRAM_IO
	dex
	cpx #0
	beq :+
	jmp more_padding
:

    lda #$C1
	sta PPU_VRAM_IO

	lda #$C3
	sta PPU_VRAM_IO

    lda #$C1
	sta PPU_VRAM_IO

	lda #$C3
	sta PPU_VRAM_IO

; End temp Code for upscale

	;lda #180
	;sta oam
	;lda #120
	;sta oam + 3
	;lda #1
	;sta oam + 1
	;lda #0
	;sta oam + 2
	jsr ppu_update

main_loop:
	lda nmi_ready
	; cmp #0 unnecessary because lda already sets this flag based on the value
	bne main_loop

	lda #1
	sta nmi_ready
	jmp main_loop
.endproc

; .out .sprintf("%d", * - irq)

